MODULE FCOperationPerformance;

REQUIRE Utils, System, FCCostCenters, FCOperations, FCSpecification, FCEmployees, FCProjects, Time;

CLASS OperationPerformanceReport 'Monthly cost center performance record';
CLASS OperationPerformancePosition 'Daily details';

//Properties of #OperationPerformanceReport:
dateFill 'Filled' = DATA DATE(OperationPerformanceReport);
dateFor 'For' = DATA DATE(OperationPerformanceReport);

customMonth = DATA CustomMonth(OperationPerformanceReport);
customMonthName 'Month' (OperationPerformanceReport r)= name(customMonth(r));

costCenter = DATA CostCenter(OperationPerformanceReport);
costCenterName 'Cost center' (OperationPerformanceReport r)= name(costCenter(r));

worker = DATA Employee(OperationPerformanceReport); // Reasonable only if a cost center is human-based. Restriction of the public code version: There is no division of human-based and machine-based cost centers
workerName 'Worker' (OperationPerformanceReport r)= name(worker(r));

workerDoesNotCorrespondToWorkplace '! Attention' (OperationPerformanceReport p) = OVERRIDE '! Worker does not correspond to workplace' IF currentWorkplace(worker(p)) != costCenter(p);

// Properties of #OperationPerformancePosition
reportN 'Operatoin report' = DATA OperationPerformanceReport(OperationPerformancePosition);

dateFor 'day' (OperationPerformancePosition p) = dateFor(reportN(p));
customMonth = DATA CustomMonth(OperationPerformancePosition);
customMonthName 'Month' (OperationPerformancePosition p)= name(customMonth(p));

worker = DATA Employee(OperationPerformancePosition);
workerName 'Worker' (OperationPerformancePosition p)= name(worker(p));

costCenter = DATA CostCenter(OperationPerformancePosition);
costCenterName 'Workplace' (OperationPerformancePosition p)= name(costCenter(p));

operation = DATA Operation(OperationPerformancePosition);
operationName 'Operation' (OperationPerformancePosition p) = name(operation(p));

hours 'O vol' = DATA NUMERIC[3,0](OperationPerformancePosition);
 
costCenterConsumer = DATA CostCenter(OperationPerformancePosition);
costCenterConsumerName 'Cost center (consumer)' (OperationPerformancePosition p) = name(costCenterConsumer(p));

projectConsumer = DATA Project(OperationPerformancePosition);
projectConsumerName 'Project' (OperationPerformancePosition p) = title(projectConsumer(p));
  
description 'Work description' = DATA STRING[75](OperationPerformancePosition);

specification = DATA Specification(OperationPerformancePosition);
specificationNumber 'BoR' (OperationPerformancePosition p)= number(specification(p));

CONSTRAINT operation(specification(OperationPerformancePosition p)) != operation(p) CHECKED BY specification MESSAGE 'You should pick a specification related to the operation';

//Properties of #OperationPerformanceReport (add):
hours 'h' (OperationPerformanceReport r) = GROUP SUM hours(OperationPerformancePosition p) BY reportN(p);

// Forms for #OperationPerformanceReport:
FORM operationPerformanceForm 'Operation performance monthly records' // #OperationPerformanceReport main form
    OBJECTS r = OperationPerformanceReport
    PROPERTIES (r) dateFill, dateFor, customMonthName, costCenterName, NEW, DELETE 

    OBJECTS p = OperationPerformancePosition
    PROPERTIES (p) customMonthName, dateFor, 'Operation' = operationName, 'CC-performer' = costCenterName, 'CC-consumer' = costCenterConsumerName, projectConsumerName, 'Technology' = specificationNumber, description, 'Amount (h)' = hours, NEW, DELETE
    FILTERS reportN(p) = r
    ORDERS hours(p), costCenterName(p), costCenterConsumerName(p), projectConsumerName(p)       
;
